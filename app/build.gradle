apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("sign/keystore.properties")
// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()
// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    // 签名配置
    signingConfigs {
        debugConfig {
            storeFile file(keystoreProperties['debugStoreFile'])
            storePassword keystoreProperties['debugStorePassword']
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
        }
        releaseConfig {
            storeFile file(keystoreProperties['releaseStoreFile'])
            storePassword keystoreProperties['releaseStorePassword']
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
        }
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.codearms.maoqiqi.sign"
        minSdkVersion 27
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    // 构建类型配置
    buildTypes {
        debug {
            debuggable true
            jniDebuggable false
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debugConfig
        }
        release {
            debuggable false // 支持断点调试和log信息打印(默认true)
            jniDebuggable false // JNI调试(默认false)
            minifyEnabled false // 压缩类文件代码(默认false)
            useProguard true // 压缩资源文件
            // 混淆文件的位置
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig // 签名配置
        }
    }

    flavorDimensions "default"

    // 产品风味配置
    productFlavors {
        personal {
            applicationId "com.codearms.maoqiqi.sign.personal"
            versionNameSuffix '-personal'
        }
        enterprise {
            applicationId "com.codearms.maoqiqi.sign.enterprise"
            versionNameSuffix '-enterprise'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
}